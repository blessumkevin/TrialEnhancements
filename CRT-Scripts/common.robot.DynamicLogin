*** Settings ***
Library                         QForce
Library                         String
Library                         Collections


*** Variables ***
${BROWSER}                      chrome
${home_url}                     ${LT_login_url}/lightning/page/home
${data_cloud_setup_url}         ${LT_login_url}/lightning/setup/SetupOneHome/home?setupApp=audience360
${salesforce_setup_url}         ${LT_login_url}/lightning/setup/SetupOneHome/home?setupApp=all

*** Keywords ***
Setup Browser
    Set Library Search Order    QWeb                        QForce
    Open Browser                about:blank                 ${BROWSER}
    SetConfig                   LineBreak                   ${EMPTY}                    #\ue000
    SetConfig                   DefaultTimeout              20s                         #sometimes salesforce is slow


End suite
    Set Library Search Order    QForce                        
    Close All Browsers


Live Testing Login
    [Documentation]             Login to Salesforce instance
    Set Library Search Order    QWeb                        QForce
    GoTo                        ${LT_login_url}
    TypeText                    Username                    ${LT_username}              delay=1
    TypeText                    Password                    ${LT_password}
    ClickText                   Log In

Dynamic Login
    [Documentation]             Login to Salesforce instance
    ${DYNAMIC_LOGIN}=           Get Variable Value          ${loginUrl}                 NoValuePassed
    IF                          '${DYNAMIC_LOGIN}' != 'NoValuePassed'
        GoTo                    ${DYNAMIC_LOGIN}
        ${current_url}=         GetUrl
        Log                     Current URL: ${current_url}
        ${instance_url}=        Get Regexp Matches          ${current_url}              (https://.*\\.force\\.com)
        ${instance_url}=        Get From List               ${instance_url}             0
        Log                     IU: ${instance_url}
        Set Global Variable     ${home_url}                 ${instance_url}/lightning/page/home
        Set Global Variable     ${salesforce_setup_url}     ${instance_url}/lightning/setup/SetupOneHome/home?setupApp=all
    ELSE
        Live Testing Login
    END

Home
    [Documentation]             Navigate to homepage, login if needed
    Set Library Search Order    QWeb                        QForce
    GoTo                        ${home_url}
    ${login_status} =           IsText                      To access this page, you have to log in to Salesforce.    2
    Run Keyword If              ${login_status}             Dynamic Login


Salesforce Setup
    Home
    GoTo                        ${salesforce_setup_url}
    VerifyText                  Setup Home
